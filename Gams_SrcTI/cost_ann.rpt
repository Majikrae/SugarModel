*=============================================================================*
* COST_ANN: annual cost calculation
*   - Investment Costs, Tax/Subsidies
*   - Decommissioning
*   - Fixed costs and taxes
*   - Variable costs and Taxes/Subsidies
*-----------------------------------------------------------------------------
*  %1 - Prefix for parameter names (optional)
*  %2 - SOW, (optional)
*=============================================================================*
  OBJ_YES(NCAP_YES)=YES;
  NCAP_YES(R,PYR_S,P)$PRC_RESID(R,'0',P) = NO;
* Levelized annual investment and fixed costs
  IF(ACL, OPTION CLEAR=FIL2;
    OPTION CLEAR=OBJ_SUMSI,CLEAR=PAR_OBJCAP; FIL2(V) = B(V)-YEARVAL(V);
    OBJ_SUMSI(RTP(R,V(LL),P),LL+(FIL2(V)+NCAP_ILED(R,V,P)))$(B(V)+NCAP_ILED(R,V,P)>=MIYR_V1) = YES;
    OBJ_SUMSI(RTP(R,V(LL),P),LL+MAX(0,MIYR_V1-YEARVAL(V)))$(B(V)+NCAP_ILED(R,V,P)<MIYR_V1) = YES;
    PAR_OBJCAP(NCAP_YES(R,V,P),CUR)$RDCUR(R,CUR) = (1-(1/(1+G_DRATE(R,V,CUR)))) / (1-(1+G_DRATE(R,V,CUR))**
        (-MAX(1,MIN(MIYR_VL+1,B(V)+NCAP_ILED(R,V,P)+NCAP_TLIFE(R,V,P)*COEF_RPTI(R,V,P))-MAX(MIYR_V1,B(V)+NCAP_ILED(R,V,P)))));
    RTP_OBJ(J,R,V,P,CUR)$RTP_OBJ(J,R,V,P,CUR) = RTP_OBJ(J,R,V,P,CUR)*SUM(OBJ_SUMSI(R,V,P,K),1/OBJ_DISC(R,K,CUR))*PAR_OBJCAP(R,V,P,CUR);
    %1CST_INVC(%2RTP_CPTYR(R,V,T,P),SYSINV)$NCAP_YES(R,V,P) =
       SUM(RDCUR(R,CUR),RTP_OBJ('1',R,V,P,CUR)*COEF_CPT(R,V,T,P)*ABS(SYSONE(SYSINV)-COEF_CRF(R,V,P,CUR)));
    %1CST_INVX(%2RTP_CPTYR(R,V,T,P),SYSINV)$NCAP_YES(R,V,P) =
       SUM(RDCUR(R,CUR),RTP_OBJ('2',R,V,P,CUR)*COEF_CPT(R,V,T,P)*ABS(SYSONE(SYSINV)-COEF_CRF(R,V,P,CUR)));
    RTP_NPV(J,R,V,P,CUR)$RTP_NPV(J,R,V,P,CUR) = RTP_NPV(J,R,V,P,CUR)*SUM(OBJ_SUMSI(R,V,P,K),1/OBJ_DISC(R,K,CUR))*PAR_OBJCAP(R,V,P,CUR);
    %1CST_FIXC(%2RTP_CPTYR(R,V,T,P))$NCAP_YES(R,V,P) = SUM(RDCUR(R,CUR),RTP_NPV('1',R,V,P,CUR)*COEF_CPT(R,V,T,P));
    %1CST_FIXX(%2RTP_CPTYR(R,V,T,P))$NCAP_YES(R,V,P) = SUM(RDCUR(R,CUR),RTP_NPV('2',R,V,P,CUR)*COEF_CPT(R,V,T,P));
   OPTION CLEAR=NCAP_YES,CLEAR=RTP_OBJ,CLEAR=RTP_NPV;
  );
*------------------------------------------------------------------------------
* Cases I - Investment Cost and II - Taxes/Subsidies
*------------------------------------------------------------------------------
* Calculate Annual undiscounted investment costs CST_INVC
$SET TMP '' SET X1 '' SETLOCAL SIC 1
$IFI '%STAGES%'==YES PASTSUM(NCAP_YES(R,T,P)) = SUM(SW_TSW(SOW,T,WW),OBJ_SIC(R,T,P,WW));
$IFI '%STAGES%'==YES $SETLOCAL SIC 1+PASTSUM(R,V,P)

LOOP(OBJ_ICUR(NCAP_YES(R,V,P),CUR), OPTION CLEAR=YKK; MY_F=%SIC%;
  SYSPLIT(SYSINV)=ABS(SYSONE(SYSINV)-COEF_CRF(R,V,P,CUR));
  F = (VAR_NCAP.L(R,V,P)$T(V)+NCAP_PASTI(R,V,P)$PYR(V)) * OBJ_CRF(R,V,P,CUR)/OBJ_DIVI(R,V,P);
  LOOP(OBJ_SUMII(R,V,P,LIFE,K_EOH,JOT), Z=ORD(LIFE);
    YKK(YK(T(YEAR),LL),K)$((ORD(YEAR) < ORD(LL)+Z)$INVSPRED(K_EOH,JOT,LL,K)$VNT(V,T)) = YES);
  %1CST_INVC(%2R,VNT(V,T),P,SYSINV) = SUM(YKK(T,LL,K), F * MY_F * OBJ_ICOST(R,K,P,CUR))*SYSPLIT(SYSINV);
  %1CST_INVX(%2R,VNT(V,T),P,SYSINV)$(OBJ_ITAX(R,V,P,CUR)+OBJ_ISUB(R,V,P,CUR)) =
       SUM(YKK(T,LL,K), F * (OBJ_ITAX(R,K,P,CUR)-OBJ_ISUB(R,K,P,CUR)))*SYSPLIT(SYSINV);
);
* Report approximate costs for RESID according to available capacity
LOOP(OBJ_ICUR(R,PASTMILE(V),P,CUR)$PRC_RESID(R,'0',P),
 %1CST_INVC(%2RTP_CPTYR(R,V,T,P),SYSINV) = ABS(SYSONE(SYSINV)-COEF_CRF(R,V,P,CUR)) *
   NCAP_PASTI(R,V,P) * COEF_CPT(R,V,T,P) * OBJ_CRF(R,V,P,CUR) * OBJ_ICOST(R,V,P,CUR);
 %1CST_INVX(%2RTP_CPTYR(R,V,T,P),SYSINV) = ABS(SYSONE(SYSINV)-COEF_CRF(R,V,P,CUR)) *
   NCAP_PASTI(R,V,P) * COEF_CPT(R,V,T,P) * OBJ_CRF(R,V,P,CUR) * (OBJ_ITAX(R,V,P,CUR)-OBJ_ISUB(R,V,P,CUR));
);

* Handle ETL
$IF NOT %ETL% == 'YES' $GOTO ETLDONE
LOOP(OBJ_ICUR(R,T,TEG(P),CUR)$VAR_IC.L(R,T,P), OPTION CLEAR=YKK;
  F = VAR_IC.L(R,T,P) * OBJ_CRF(R,T,P,CUR)/OBJ_DIVI(R,T,P);
  LOOP((OBJ_SUMII(R,T,P,LIFE,K_EOH,JOT),INVSPRED(K_EOH,JOT,LL,K)),
    Z=ORD(LIFE); YKK(YK(TT,LL),K)$(YEARVAL(TT) < YEARVAL(LL)+Z) = YES);
  %1CST_INVC(%2R,VNT(T,TT),P,SYSINV)$SYSONE(SYSINV) = %1CST_INVC(%2R,T,TT,P,SYSINV)+SUM(YKK(TT,LL,K),F);
);
$LABEL ETLDONE

*------------------------------------------------------------------------------
* Cases III - Decommissioning
*------------------------------------------------------------------------------
OPTION OBJ_SUMSI <= OBJ_SUMIII;
* Calculate decommissioning costs, annualized to operating years:
LOOP(OBJ_ICUR(NCAP_YES(R,V,P),CUR)$OBJ_DCOST(R,V,P,CUR), F = VAR_NCAP.L(R,V,P)$T(V)+NCAP_PASTI(R,V,P)$PYR(V);
  F = F*OBJ_CRF(R,V,P,CUR)/OBJ_DIVIII(R,V,P); Z = ROUND(NCAP_%INVLIF%(R,V,P));
  %1CST_DECC(%2R,VNT(V,T),P) = SUM(OBJ_SUMSI(R,V,P,LL)$((YEARVAL(T) < YEARVAL(LL)+Z)$YK(T,LL)),
      (SUM(OBJ_SUMIII(R,V,P,LL,K,Y),OBJ_DISC(R,Y,CUR)*OBJ_DCOST(R,K,P,CUR))*COR_SALVD(R,V,P,CUR) +
       SUM(OBJ_SUMIVS(R,V,P,K(LL),Y),OBJ_DISC(R,Y,CUR))*OBJ_DLAGC(R,LL,P,CUR))*F/OBJ_DISC(R,LL,CUR));
);

*------------------------------------------------------------------------------
* Cases IV - Fixed costs
*------------------------------------------------------------------------------

 LOOP(OBJ_FCUR(NCAP_YES(R,V,P),CUR), F = VAR_NCAP.L(R,V,P)$T(V)+NCAP_PASTI(R,V,P)$PYR(V);
   F = F / OBJ_DIVIV(R,V,P); OPTION CLEAR=YKAGE;
   LOOP(OBJ_SUMIV(K_EOH,R,V,P,JOT,LIFE), Z=ORD(LIFE)-1;
     YKAGE(YK(T(YEAR),LL),AGE(LIFE+(ORD(YEAR)-ORD(LL)-Z)))$(OPYEAR(LIFE,AGE)$INVSTEP(K_EOH,JOT,LL,JOT)$VNT(V,T)) = YES);
   %1CST_FIXC(%2R,VNT(V,T),P) =
     SUM(YKAGE(T,K,AGE), F * OBJ_FOM(R,K,P,CUR) * (1+SUM(RTP_SHAPE(R,V,P,'1',J,JJ),SHAPE(J,AGE)*MULTI(JJ,T)-1)));
   %1CST_FIXX(%2R,VNT(V,T),P)$(OBJ_FTX(R,V,P,CUR)+OBJ_FSB(R,V,P,CUR)) =
     SUM(YKAGE(T,K,AGE), F * (OBJ_FTX(R,K,P,CUR) * (1+SUM(RTP_SHAPE(R,V,P,'2',J,JJ),SHAPE(J,AGE)*MULTI(JJ,T)-1)) -
                              OBJ_FSB(R,K,P,CUR) * (1+SUM(RTP_SHAPE(R,V,P,'3',J,JJ),SHAPE(J,AGE)*MULTI(JJ,T)-1))));
);
* Report approximate costs for RESID according to available capacity
LOOP(OBJ_FCUR(R,PASTMILE(V),P,CUR)$PRC_RESID(R,'0',P),
 %1CST_FIXC(%2RTP_CPTYR(R,V,T,P)) = NCAP_PASTI(R,V,P)*COEF_CPT(R,V,T,P) * OBJ_FOM(R,V,P,CUR) * (1+SUM(RTP_SHAPE(R,V,P,'1',J,JJ),MULTI(JJ,T)-1));
 %1CST_FIXX(%2RTP_CPTYR(R,V,T,P)) = NCAP_PASTI(R,V,P)*COEF_CPT(R,V,T,P) *(OBJ_FTX(R,V,P,CUR) * (1+SUM(RTP_SHAPE(R,V,P,'2',J,JJ),MULTI(JJ,T)-1)) -
                                                                          OBJ_FSB(R,V,P,CUR) * (1+SUM(RTP_SHAPE(R,V,P,'3',J,JJ),MULTI(JJ,T)-1)));
);

 NCAP_YES(OBJ_YES)=YES;
$IF NOT DEFINED PRC_RCAP $GOTO RETDONE
 OPTION CLEAR=COEF_CAP;
 COEF_CAP(RTP_CPTYR(R,V,T,P))$(NCAP_YES(R,V,P)$PRC_RCAP(R,P)) = 1-VAR_SCAP.L(R,V,T,P)/(VAR_NCAP.L(R,V,P)+NCAP_PASTI(R,V,P))+EPS;
 %1CST_FIXC(%2RTP_CPTYR(R,V,T,P)) $= COEF_CAP(R,V,T,P)*%1CST_FIXC(%2R,V,T,P);
 %1CST_FIXX(%2RTP_CPTYR(R,V,T,P)) $= COEF_CAP(R,V,T,P)*%1CST_FIXX(%2R,V,T,P);
 COEF_CPT(R,V,T,P)$RTFORC(R,V,T,P) = 1-RTFORC(R,V,T,P)/NCAP_PASTI(R,V,P);
$LABEL RETDONE

*-----------------------------------------------------------------------------
* Marginal costs of endogenous trade
*-----------------------------------------------------------------------------
  OPTION CLEAR=OBJ_YES,CLEAR=PAR_IPRIC;
  PAR_IPRIC(RTP(R,T,P),C,TS,'IMP') $= (-1) * EQ_IRE.M(R,T,P,C,'IMP',TS);
  CG_GRP(R,P,C,COM)$TOP_IRE(R,C,R,COM,P) = YES;
  LOOP(CG_GRP(REG,P,COM1,COM)$RPC_MARKET(REG,P,COM,'IMP'),
    PAR_IPRIC(RTPCS_VARF(R,T,P,C,TS),'IMP')$TOP_IRE(REG,COM1,R,C,P) =
      -SUM((RS_TREE(R,ALL_TS,TS),S)$(IRE_TSCVT(R,ALL_TS,REG,S)$EQ_IRE.M(REG,T,P,COM,'IMP',S)),
               EQ_IRE.M(REG,T,P,COM,'IMP',S) *
               IRE_CCVT(REG,COM1,REG,COM) * IRE_CCVT(R,C,REG,COM1) * RS_FR(R,ALL_TS,TS) * IRE_TSCVT(R,ALL_TS,REG,S)));

  PAR_IPRIC(RTPCS_VARF(R,T,P,C,TS),'EXP')$RPC_IREIO(R,P,C,'EXP','IN') $=
    SMAX(TOP_IRE(R,C,REG,COM,P),SUM((RS_TREE(R,TS,ALL_TS),S)$(IRE_TSCVT(R,ALL_TS,REG,S)$EQ_IRE.M(REG,T,P,COM,'IMP',S)),
               EQ_IRE.M(REG,T,P,COM,'IMP',S) * IRE_FLO(R,T,P,C,REG,COM,S) *
               IRE_CCVT(R,C,REG,COM) * RS_FR(R,ALL_TS,TS) * IRE_TSCVT(R,ALL_TS,REG,S)));

  PAR_IPRIC(RTP(R,T,P),C,TS,'EXP') $= EQ_IRE.M(R,T,P,C,'EXP',TS);
  LOOP(RPC_EQIRE(REG,P,COM,'EXP'),
    PAR_IPRIC(RTPCS_VARF(R,T,P,C,TS),'IMP')$TOP_IRE(REG,COM,R,C,P) =
      -SUM((RS_TREE(R,ALL_TS,TS),S)$IRE_TSCVT(R,ALL_TS,REG,S),
               EQ_IRE.M(REG,T,P,COM,'EXP',S) / IRE_FLO(REG,T,P,COM,R,C,TS) *
               IRE_CCVT(R,C,REG,COM) * RS_FR(R,ALL_TS,TS) * IRE_TSCVT(R,ALL_TS,REG,S)));

  PRC_YMAX(RP_IRE(R,P)) $= SUM((RPC_IREIO(R,P,C,IE,'IN'),RTP_VINTYR(R,V,T,P),RPCS_VAR(R,P,C,S)),
                                PAR_IPRIC(R,T,P,C,S,IE)*PAR_IRE(R,V,T,P,C,S,IE));
  %1REG_IREC(%2R) $= SUM(P,PRC_YMAX(R,P));
  PAR_IPRIC(R,T,P,C,TS,IE)$PAR_IPRIC(R,T,P,C,TS,IE) = PAR_IPRIC(R,T,P,C,TS,IE)*(1/COEF_PVT(R,T));
$IFI %STAGES%==YES  %1PAR_IPRIC(%2R,T,P,C,TS,IE) $= PAR_IPRIC(R,T,P,C,TS,IE);
  OPTION CLEAR=CG_GRP;

*------------------------------------------------------------------------------
* Marginal costs associated with endogenous imports/exports
* - note that price only applied when actually an internal region
*------------------------------------------------------------------------------
  %1CST_IREC(%2RTP_VINTYR(R,V,T,P),C)$(RPC(R,P,C)$RP_IRE(R,P)) $=
     SUM((RTPCS_VARF(R,T,P,C,S),RPC_IREIO(R,P,C,IE,'IN')),PAR_IPRIC(R,T,P,C,S,IE)*PAR_IRE(R,V,T,P,C,S,IE));

*------------------------------------------------------------------------------
* EQOBJVAR the objective function variable cost reporting
*------------------------------------------------------------------------------
$IFI %ANNCOST%==LEV $SET X1 'LEV' SET TMP *(1/OBJ_PVT(R,T,CUR))
  %1CST_TIME(%2R,T,ANNUAL,'%SYSPREFIX%%X1%COST') = VDA_DISC(R,T);
$IFI NOT %ANNCOST%==LEV $BATINCLUDE eqobjvar.rpt PAR J('1'), J('2'), T
  %1CST_ACTC(%2R,V,T,P,'-') $= SUM(CUR$PAR_ACTC('1',R,V,T,P,%PGPRIM%,CUR),PAR_ACTC('1',R,V,T,P,%PGPRIM%,CUR)%TMP%);
  %1CST_ACTC(%2R,V,T,P,'+') $= SUM(CUR$PAR_ACTC('2',R,V,T,P,%PGPRIM%,CUR),PAR_ACTC('2',R,V,T,P,%PGPRIM%,CUR)%TMP%);
  %1CST_FLOC(%2R,V,T,P,C) $= SUM(CUR$PAR_FLOC('1',R,V,T,P,C,CUR),PAR_FLOC('1',R,V,T,P,C,CUR)%TMP%);
  %1CST_FLOX(%2R,V,T,P,C) $= SUM(CUR$PAR_FLOC('2',R,V,T,P,C,CUR),PAR_FLOC('2',R,V,T,P,C,CUR)%TMP%);
  %1CST_COMC(%2R,T,C)     $= SUM(CUR$PAR_COMC('1',R,T,C,CUR),PAR_COMC('1',R,T,C,CUR)%TMP%);
  %1CST_COMX(%2R,T,C)     $= SUM(CUR$PAR_COMC('2',R,T,C,CUR),PAR_COMC('2',R,T,C,CUR)%TMP%);

$IF DEFINED OBJ_COMBAL $BATINCLUDE powerflo.vda RPTB '' '' '%1' "%2" "%SOW%"
*------------------------------------------------------------------------------
* EQOBJELS the objective function flexible demand cost reporting
*------------------------------------------------------------------------------
$ BATINCLUDE eqobjels.rpt %1CST_COME(%2R,TT,C) TT
  IF(CARD(%1CST_PVP),
   %1CST_PVC(%2'%SYSPREFIX%ELS',R,C)$SUM(RCJ(R,C,'1',BDNEQ),1) = SUM(T,%1CST_COME(%2R,T,C)*COEF_PVT(R,T));
   %1CST_PVP(%2'%SYSPREFIX%IRE',RP_IRE(R,P)) $= PRC_YMAX(R,P);
  );
*------------------------------------------------------------------------------
* Lump-sum investment costs
*------------------------------------------------------------------------------
$SET P1 "'%SYSPREFIX%INSTCAP'"
  %1CAP_NEW(%2RVPT(NCAP_YES(R,T,P),TT),%P1%) = VAR_NCAP.L(R,T,P);
  %1CAP_NEW(%2NCAP_YES(R,T,P),T,%P1%)$(NOT OBJ_2A(R,T,P)) = COEF_RPTI(R,T,P)*VAR_NCAP.L(R,T,P);
  OPTION CLEAR=PAR_OBJCAP;
  PAR_OBJCAP(OBJ_ICUR(NCAP_YES(R,T,P),CUR)) =
    SUM((OBJ_SUMII(R,T,P,LIFE,K_EOH,JOT),INVSPRED(K_EOH,JOT,Y,K)),
        (OBJ_ICOST(R,K,P,CUR)+OBJ_ITAX(R,K,P,CUR)-OBJ_ISUB(R,K,P,CUR))) *
    VAR_NCAP.L(R,T,P) * COR_SALVI(R,T,P,CUR) / OBJ_DIVI(R,T,P) /
    (1+G_DRATE(R,T,CUR))**(%DISCSHIFT%$(NOT NCAP_ILED(R,T,P)));
  SYSPLIT(SUCL)=1; IF(CARD(SYSINV)>1,OPTION SUCL<SYSINV,SYSPLIT<SYSONE);
  %1CAP_NEW(%2R,T,P,TT,SUCL)$%1CAP_NEW(%2R,T,P,TT,%P1%)=SUM(RDCUR(R,CUR),PAR_OBJCAP(R,T,P,CUR)*ABS(SYSPLIT(SUCL)-COEF_CRF(R,T,P,CUR)));
*------------------------------------------------------------------------------
* Regional annual costs
  %1REG_ACOST(%2R,T,'INV') =  SUM((VNT(V,T),P,SYSINV)$%1CST_INVC(%2R,V,T,P,SYSINV),%1CST_INVC(%2R,V,T,P,SYSINV));
  %1REG_ACOST(%2R,T,'INVX') = SUM((VNT(V,T),P,SYSINV)$%1CST_INVX(%2R,V,T,P,SYSINV),%1CST_INVX(%2R,V,T,P,SYSINV));
  %1REG_ACOST(%2R,T,'FIX') =  SUM((VNT(V,T),P)$%1CST_FIXC(%2R,V,T,P),%1CST_FIXC(%2R,V,T,P));
  %1REG_ACOST(%2R,T,'FIXX') = SUM((VNT(V,T),P)$%1CST_FIXX(%2R,V,T,P),%1CST_FIXX(%2R,V,T,P));
  %1REG_ACOST(%2R,T,'VAR') =  SUM((VNT(V,T),P,RPM)$%1CST_ACTC(%2R,V,T,P,RPM),%1CST_ACTC(%2R,V,T,P,RPM)) +
                              SUM((VNT(V,T),P,C)$%1CST_FLOC(%2R,V,T,P,C),%1CST_FLOC(%2R,V,T,P,C)) +
                              SUM(C$%1CST_COMC(%2R,T,C),%1CST_COMC(%2R,T,C));
  %1REG_ACOST(%2R,T,'VARX') = SUM((VNT(V,T),P,C)$%1CST_FLOX(%2R,V,T,P,C),%1CST_FLOX(%2R,V,T,P,C)) +
                              SUM(C$%1CST_COMX(%2R,T,C),%1CST_COMX(%2R,T,C));
  %1REG_ACOST(%2R,T,'IRE') =  SUM((VNT(V,T),P,C)$%1CST_IREC(%2R,V,T,P,C),%1CST_IREC(%2R,V,T,P,C));
  %1REG_ACOST(%2R,T,'ELS') =  SUM(C$%1CST_COME(%2R,T,C),%1CST_COME(%2R,T,C));
*------------------------------------------------------------------------------
  OPTION CLEAR=PAR_ACTC,CLEAR=PAR_COMC,CLEAR=PAR_FLOC,CLEAR=PRC_YMAX,CLEAR=PASTSUM,CLEAR=COEF_CRF,CLEAR=PAR_OBJCAP;
$IF %1==S $EXIT

*------------------------------------------------------------------------------
* Salvage costs (not for stochastic runs)
*------------------------------------------------------------------------------
* Calculate actual Salvage values
  PAR_OBJSAL(OBJ_ICUR(R,T,P,CUR)) = PAR_OBJSAL(R,T,P,CUR)*VAR_NCAP.L(R,T,P);
$IF %ETL% == 'YES'
  PAR_OBJSAL(RTP(R,T,TEG(P)),CUR)$G_RCUR(R,CUR) = PAR_OBJSAL(R,T,P,CUR)+SUM(OBJ_SUMS(R,T,P),OBJSIC(R,T,P)*VAR_IC.L(R,T,P))*OBJ_DCEOH(R,CUR);
  %1CST_SALV(%2R,T,P) $= SUM(RDCUR(R,CUR),PAR_OBJSAL(R,T,P,CUR));
  REG_OBJ(R) = SUM((RDCUR(R,CUR),ITEM,OBV)$SUM_OBJ(ITEM,OBV),VAR_OBJ.L(R,OBV,CUR)*SUM_OBJ(ITEM,OBV));
